message(STATUS "Setup OTB...")

set(proj OTB)

set(OTB_SB_BUILD_DIR ${CMAKE_BINARY_DIR}/${proj}/build)

set(${proj}_DEPENDENCIES)

if(USE_SYSTEM_GDAL)
  set(OTB_SB_GDAL_CONFIG)
else()
  set(OTB_SB_GDAL_CONFIG
    -DGDAL_INCLUDE_DIR:PATH=${CMAKE_INSTALL_PREFIX}/include
    -DGDAL_LIBRARY:PATH=${CMAKE_INSTALL_PREFIX}/lib/libgdal.so
    -DGDAL_CONFIG:PATH=${CMAKE_INSTALL_PREFIX}/bin/gdal-config
    )
  list(APPEND ${proj}_DEPENDENCIES GDAL)
endif()

if(USE_SYSTEM_OSSIM)
  set(OTB_SB_OSSIM_CONFIG)
else()
  set(OTB_SB_OSSIM_CONFIG
    -DOSSIM_INCLUDE_DIR:PATH=${CMAKE_INSTALL_PREFIX}/include
    -DOSSIM_LIBRARY:PATH=${CMAKE_INSTALL_PREFIX}/lib/libossim.so
    )
  list(APPEND ${proj}_DEPENDENCIES OSSIM)
endif()

if(USE_SYSTEM_ITK)
  set(OTB_SB_ITK_CONFIG)
else()
  set(OTB_SB_ITK_CONFIG
    -DITK_DIR:PATH=${CMAKE_INSTALL_PREFIX}/lib/cmake/ITK-4.6
    )
  list(APPEND ${proj}_DEPENDENCIES ITK)
endif()

if(USE_SYSTEM_MUPARSER)
  set(OTB_SB_MUPARSER_CONFIG)
else()
  set(OTB_SB_MUPARSER_CONFIG
    -DMUPARSER_INCLUDE_DIR:PATH=${CMAKE_INSTALL_PREFIX}/include
    -DMUPARSER_LIBRARY:PATH=${CMAKE_INSTALL_PREFIX}/lib/libmuparser.so
    )
  list(APPEND ${proj}_DEPENDENCIES MUPARSER)
endif()

if(USE_SYSTEM_MUPARSERX)
  set(OTB_SB_MUPARSERX_CONFIG)
else()
  set(OTB_SB_MUPARSERX_CONFIG
    -DMUPARSERX_INCLUDE_DIR:PATH=${CMAKE_INSTALL_PREFIX}/include
    -DMUPARSERX_LIBRARY:PATH=${CMAKE_INSTALL_PREFIX}/lib/libmuparserx.so
    )
  list(APPEND ${proj}_DEPENDENCIES MUPARSERX)
endif()

if(USE_SYSTEM_TINYXML)
  set(OTB_SB_TINYXML_CONFIG)
else()
  set(OTB_SB_TINYXML_CONFIG
    -DTINYXML_INCLUDE_DIR:PATH=${CMAKE_INSTALL_PREFIX}/include
    -DTINYXML_LIBRARY:PATH=${CMAKE_INSTALL_PREFIX}/lib/libtinyXML.so
    )
  list(APPEND ${proj}_DEPENDENCIES TINYXML)
endif()

if(USE_SYSTEM_BOOST)
  set(OTB_SB_BOOST_CONFIG)
else()
  set(OTB_SB_BOOST_CONFIG
    -DBOOST_ROOT:PATH=${CMAKE_INSTALL_PREFIX}
    )
  list(APPEND ${proj}_DEPENDENCIES BOOST)
endif()

if(USE_SYSTEM_LIBKML)
  set(OTB_SB_LIBKML_CONFIG)
else()
  set(OTB_SB_LIBKML_CONFIG
    # TODO : split LIBKML build into 7 libraries
    -DLIBKML_INCLUDE_DIR:PATH=${CMAKE_INSTALL_PREFIX}/include
    -DLIBKML_BASE_LIBRARY:PATH=${CMAKE_INSTALL_PREFIX}/lib/libkmlbase.so
    -DLIBKML_CONVENIENCE_LIBRARY:PATH=${CMAKE_INSTALL_PREFIX}/lib/libkmlconvenience.so
    -DLIBKML_DOM_LIBRARY:PATH=${CMAKE_INSTALL_PREFIX}/lib/libkmldom.so
    -DLIBKML_ENGINE_LIBRARY:PATH=${CMAKE_INSTALL_PREFIX}/lib/libkmlengine.so
    -DLIBKML_REGIONATOR_LIBRARY:PATH=${CMAKE_INSTALL_PREFIX}/lib/libkmlregionator.so
    -DLIBKML_XSD_LIBRARY:PATH=${CMAKE_INSTALL_PREFIX}/lib/libkmlxsd.so
    -DLIBKML_MINIZIP_LIBRARY:PATH=${CMAKE_INSTALL_PREFIX}/lib/libminizip.so
    )
  list(APPEND ${proj}_DEPENDENCIES LIBKML)
endif()

if(USE_SYSTEM_OPENCV)
  set(OTB_SB_OPENCV_CONFIG)
else()
  set(OTB_SB_OPENCV_CONFIG
    -DOpenCV_DIR:PATH=${CMAKE_INSTALL_PREFIX}/share/OpenCV
    )
  list(APPEND ${proj}_DEPENDENCIES OPENCV)
endif()

ExternalProject_Add(${proj}
    DEPENDS ${${proj}_DEPENDENCIES}
    PREFIX ${proj}
    HG_REPOSITORY "http://hg.orfeo-toolbox.org/OTB"
    HG_TAG tip
    BINARY_DIR ${OTB_SB_BUILD_DIR}
    INSTALL_DIR ${CMAKE_INSTALL_PREFIX}
    CMAKE_CACHE_ARGS
      -DCMAKE_INSTALL_PREFIX:STRING=${CMAKE_INSTALL_PREFIX}
      -DCMAKE_BUILD_TYPE:STRING=Release
      -DBUILD_SHARED_LIBS:BOOL=ON
      -DBUILD_TESTING:BOOL=${BUILD_TESTING}
      -DBUILD_EXAMPLES:BOOL=ON
      -DBUILD_APPLICATIONS:BOOL=ON
      -DOTB_WRAP_QT:BOOL=OFF
      -DCMAKE_PREFIX_PATH:STRING=${CMAKE_INSTALL_PREFIX}
      -DOTB_DATA_ROOT:STRING=${OTB_DATA_ROOT}
      -DOTB_USE_EXTERNAL_OSSIM:BOOL=ON
      -DOTB_USE_EXTERNAL_LIBKML:BOOL=ON
      -DOTB_USE_EXTERNAL_BOOST:BOOL=ON
      -DOTB_USE_OPENCV:BOOL=ON
      ${OTB_SB_ITK_CONFIG}
      ${OTB_SB_OSSIM_CONFIG}
      ${OTB_SB_GDAL_CONFIG}
      ${OTB_SB_MUPARSER_CONFIG}
      ${OTB_SB_MUPARSERX_CONFIG}
      ${OTB_SB_TINYXML_CONFIG}
      ${OTB_SB_BOOST_CONFIG}
      ${OTB_SB_LIBKML_CONFIG}
      ${OTB_SB_OPENCV_CONFIG}
    CMAKE_COMMAND
      # use 'env' because CTest launcher doesn't perform shell interpretation
      env LD_LIBRARY_PATH=${CMAKE_INSTALL_PREFIX}/lib
      ${CMAKE_COMMAND}
    )

message(STATUS "  Using OTB SuperBuild version")
